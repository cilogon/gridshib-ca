#!@PERL@ @PERL_ARGS@
######################################################################
#
# $Id$
#
# Create a CA cert and key for GridShib CA to use.
#
######################################################################
# Copyright 2006 The Board of Trustees of the University of Illinois.
# All rights reserved.

# Developed by:

#   The GridShib Project
#   National Center for Supercomputing Applications
#   University of Illinois
#   http://gridshib.globus.org/

# Permission is hereby granted, free of charge, to any person obtaining
# a copy of this software and associated documentation files (the
# "Software"), to deal with the Software without restriction, including
# without limitation the rights to use, copy, modify, merge, publish,
# distribute, sublicense, and/or sell copies of the Software, and to
# permit persons to whom the Software is furnished to do so, subject to
# the following conditions:

#   Redistributions of source code must retain the above copyright
#   notice, this list of conditions and the following disclaimers.

#   Redistributions in binary form must reproduce the above copyright
#   notice, this list of conditions and the following disclaimers in the
#   documentation and/or other materials provided with the distribution.

#   Neither the names of the National Center for Supercomputing
#   Applications, the University of Illinois, nor the names of its
#   contributors may be used to endorse or promote products derived from
#   this Software without specific prior written permission.

# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
# EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
# MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
# IN NO EVENT SHALL THE CONTRIBUTORS OR COPYRIGHT HOLDERS BE LIABLE FOR
# ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF
# CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION
# WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS WITH THE SOFTWARE.
######################################################################

=head1 create-openssl-ca.pl

Creates certificates and keys for using the GridShibCA with an
OpenSSL-based CA.

=head2 Usage

create-openssl-ca.pl [<options>]

B<Options>:

I<-h>                  Print help and exit.

I<-k>                 CA key length in bits [Default is 2048]

I<-l> E<lt>lifetimeE<gt>       CA Lifetime in days [Default is 10 years]

=head2 Return Value

Zero on success, one on error.

=head2 Version

GridShib-CA version @PACKAGE_VERSION_STRING@ ($Id$)

=cut

######################################################################

use GridShibCA::Config;
my $config = GridShibCA::Config->new();
my $ca = $config->getCA();

my $openssl = $config->getParam("OpenSSL");
my $confPath = $config->getParam("GridShibCAConfDir");
my $certFile = $config->getParam("OpenSSLCACert");
my $keyFile = $config->getParam("OpenSSLCAKey");

######################################################################

use Getopt::Std;


# Default CA lifetime is 10 years
my $lifetime = sprintf("%d", 10 * 365);

# Key length in bits
my $keyLength = 2048;

my $usage = <<"EOF";
Usage: $0 [<options>]

Options are:
 -h                  Print help
 -k                  CA key length in bits [Default is $keyLength]
 -l <lifetime>       CA Lifetime in days [Default is $lifetime]
EOF

my %options;
getopts('hl:k:', \%options);

if ($options{h})
{
    print $usage;
    exit(0);
}

if ($options{k})
{
    $keyLength = $options{k};
}

if ($options{l})
{
    $lifetime = $options{l};
}

######################################################################

# Get our relative DN
my $relativeDN = $ca->getRelativeOpenSSLDN();

# Get our CA DN
my $subject = $ca->getCAOpenSSLDN();

######################################################################

umask(0077);

######################################################################

print "Creating GridShib CA\n";
print " Keyfile is $keyFile\n";
print " Certificate is $certFile\n";
print " DN for CA is $subject\n";
print " Relative DN is $relativeDN\n";
print "\n";
print "Generated keys. This may take a minute...\n";

my @args = ();
push(@args, "req");
push(@args, "-x509");
push(@args, "-newkey", "rsa:$keyLength");
push(@args, "-out", $certFile);
push(@args, "-keyout", $keyFile);
push(@args, "-days", $lifetime);
push(@args, "-nodes");
push(@args, "-subj", $subject);

my $cmd = $config->getCommand($openssl, @args);

if (!$cmd->exec())
{
    print "Execution of $openssl failed: " . $cmd->getError();
    exit(1);
}

######################################################################

print "Getting hash of GridShib CA\n";

@args = ();
push(@args, "x509");
push(@args, "-in", $certFile);
push(@args, "-hash");
push(@args, "-noout");

$cmd = $config->getCommand($openssl, @args);

if (!$cmd->exec())
{
    print "Execution of $openssl failed: " . $cmd->getError();
    exit(1);
}
my $hash = $cmd->getOutput();
chomp($hash);

print " Hash is $hash\n";

######################################################################

use File::Copy;

my $hashFile = "${confPath}/${hash}.0";

print "Copying CA certificate to $hashFile\n";

if (copy($certFile, $hashFile) == 0)
{
    print "Copy failed: $!\n";
    exit(1);
}

######################################################################

my $policyFile = "${confPath}/${hash}.signing_policy";
my $signingSpace = $relativeDN . "/*";

# Remove duplicate /'s
$signingSpace =~ s/\/\//\//g;

print "Creating $policyFile\n";
print " Signing space is $signingSpace\n";

if (open(FH, ">$policyFile") == 0)
{
    print "Open of $policyFile failed: $!";
    exit(1);
}

my $policy = <<END;
access_id_CA      X509         '$subject'

pos_rights        globus        CA:sign

cond_subjects     globus     '"$signingSpace"'
END

print FH $policy;
close(FH);

######################################################################

my $serialFile = "${confPath}/serial";

print "Creating $serialFile\n";

if (open(FH, ">$serialFile") == 0)
{
    print "Open of $serialFile failed: $!";
    exit(1);
}

print FH "01\n";
close(FH);

######################################################################
#
# Change ownership of all created files
#

print "Setting ownership of created files.\n";
@args=();
push(@args, "@WWW_USER@");
push(@args, $certFile, $keyFile, $hashFile, $policyFile, $serialFile);

$cmd = $config->getCommand("chown", @args);

if (!$cmd->exec())
{
    print "Execution of chown failed: " . $cmd->getError();
    exit(1);
}

######################################################################

print "Done.\n";
exit(0);


### Local Variables: ***
### mode:perl ***
### End: ***
######################################################################
