######################################################################
#
# This file is part of the GriShib-CA distribution.  Copyright
# 2006-2009 The Board of Trustees of the University of
# Illinois. Please see LICENSE at the root of the distribution.
#
######################################################################

=head1 GridShibCA::ErrorHandler

Handle various errors, handling both logging and response to client.

Public methods are exported, so one can use package in follow form:

use GridShibCA::ErrorHandler qw(handleError);

=cut

package GridShibCA::ErrorHandler;

# Allow user of this package to export routines directly into their namespace
require Exporter;
@ISA = qw(Exporter);
@EXPORT_OK = qw(handleError logError htmlError htmlErrorAndExit);

use CGI;
use GridShibCA::Exception qw(:try);

=head2 Methods

=over 4

=item handleError()

Handle a serious error. This includes logging and response to client.

B<Arguments:>

C<$message> Error message

Optional arguments:

C<-exception> Causal exception

C<-exitCode> Exit code

B<Returns:> Does not return, invokes exit()

=cut

sub handleError
{
    my $message = shift || "No message given";
    my %args = @_;
    my $ex = $args{-exception};

    logError($message, $ex);

    my $cgi = CGI->new();
    if ($cgi->Accept("text/html"))
    {
	htmlError($message, $ex);
    }
    else
    {
	textError($message, $ex);
    }

    my $exitCode = $args{-exitCode} || 1;
    exit($exitCode);
}

=item logError()

Handle logging of error.

B<Arguments:>

C<$message> Error message

C<$ex> Optional exception

B<Returns:> Nothing

=cut

sub logError
{
    my $message = shift || "No message given";
    my $ex = shift; # Optional

    try
    {
	my $config = GridShibCA::Config->new();
	my $logger = $config->getLogger();

	$logger->err("Error: $message");
	if (defined($ex))
	{
	    $logger->logException($ex);
	}
    }
    otherwise
    {
	my $logEx = shift;

	# Graceful logging failed. Fall back to STDERR so it ends up
	# in Apache error log.
	print STDERR "Logging failed handling error: $message";
	if (defined($ex))
	{
	    print STDERR "EXCEPTION: " . $ex->text() . "\n";
	    if ($ex->stacktrace())
	    {
		print STDERR $ex->stacktrace();
	    }
	}
	if (defined($logEx))
	{
	    print STDERR "Logging error: " . $logEx->text() . "\n";
	}
	else
	{
	    # This shouldn't happen
	    print STDERR "Unknown logging error.\n";
	}
    };
}

=item htmlErrorAndExit

Display an HTML error to client and then exit with a status of 1.

B<Arguments:>

C<$message> Error message

B<Returns:> Doesn't. Calls exit().

=cut

sub htmlErrorAndExit
{
    my $message = shift || "No message given";

    htmlError($message);
    exit(1);
}

=item htmlError()

Handle displaying of a HTML-formatted error to client.

B<Arguments:>

C<$message> Error message

C<$ex> Exception (optional)

B<Returns:> Nothing

=cut

sub htmlError
{
    my $message = shift || "No message given";
    my $ex = shift;

    try
    {
	my $config = GridShibCA::Config->new();
	my $template = $config->getHTMLTemplate("ErrorTemplate");
	my %mappings = (
	    "Error" => $message,
	    );
	$template->addMappings(%mappings);
	print $template->getText();
    }
    otherwise
    {
	my $htmlEx = shift;

	# Nice HTML output failed. Fall back to simple version
	print "Content-type: text/plain\n\n";
	print "Sorry, but an problem occurred and the normal reporting mechanism failed!\n";
	print "The original error was: " . $message . "\n";
	print "The error reporting the problem was: " . $htmlEx->text() . "\n";
	print "Please report this to the site administrator.\n";

	# And log for service administrator
	logError("Failure generating HTML error output for user", $htmlEx);
    };
}

=item textError()

Handle displaying of a plain text error to client.

B<Arguments:>

C<$message> Error message

C<$ex> Exception (optional)

B<Returns:> Nothing

=cut

sub textError
{
    my $message = shift || "No message given";
    my $ex = shift;

    chomp($message);
    my $cgi = CGI->new();;

    # 500 Internal Server Error
    print $cgi->header(-type=>"text/plain",
		       -status=>"500 GridShib-CA Internal Error: $message");
}

=back

=head2 Version

GridShib-CA version @PACKAGE_VERSION_STRING@

=cut

# Return true for import/use
1;

### Local Variables: ***
### mode:perl ***
### End: ***
######################################################################
