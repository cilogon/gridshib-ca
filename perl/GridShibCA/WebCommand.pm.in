######################################################################
#
# This file is part of the GriShib-CA distribution.  Copyright
# 2006-2009 The Board of Trustees of the University of
# Illinois. Please see LICENSE at the root of the distribution.
#
######################################################################

=head1 GridShibCA::WebCommand

Base class for Web Commands to be invoked by CGI scripts.

=cut

package GridShibCA::WebCommand;

use Carp;
use GridShibCA::CGISession;  # For exceptions
use GridShibCA::Config;
use GridShibCA::ErrorHandler qw(handleError);
use GridShibCA::Exception qw(:try);

=head2 Configuration

A WebCommand is configued in gridshib-ca.conf as with a block in the
I<Commands> section as follows:

  <Commands>
    <Debug>
      class = GridshibCA::DisplayTemplate
      # Class-specific parameter
      template = DebugTemplate
      # Requirements
      requirements = session_optional
    </Debug>
  </Command>

Options for requirements are:

=over 4

=item nothing

No requirements.

=item https_optional

HTTPS is not required for this command. If this option is not given,
HTTPS is required.

=item session_optional

Session is established if possible, but not required.  WebCommand is
responsible for any logic that requires a session. If this option is
not given, a valid session is required.

=item csrf

CSRF check required. see L<GridShibCA::CSRF>.

=item post

Method must be POST.

=back

=cut

our $DEFAULT_REQUIREMENTS = 0x0000;
our $OPTIONAL_HTTPS = 0x001;
# 0x0002 is currenly unused
our $OPTIONAL_SESSION = 0x0004;
our $REQUIRE_CSRF_CHECK = 0x0008;
our $REQUIRE_POST = 0x0010;

%RequirementsMappings = (
    none => $REQUIRE_NOTHING,
    https_optional => $OPTIONAL_HTTPS,
    session => $REQUIRE_SESSION,
    session_optional => $OPTIONAL_SESSION,
    csrf => $REQUIRE_CSRF_CHECK,
    post => $REQUIRE_POST,
    );

=head2 Methods

=over 4

=item new()

Create a new WebCommand object.

B<Arguments:> Hash of elements from Commands entry.

B<Returns:> WebCommand instance. Throws GridShibCA::Exception on error.

=cut

sub new
{
    my $class = shift;
    my %args = @_;
    my $self = {};
    bless($self, $class);
    $self->{config} = GridShibCA::Config->new();
    $self->{logger} = $self->{config}->getLogger();
    $self->{cgi} = $self->{config}->getCGI();
    $self->{args} = \%args;
    $self->{requirements} = $self->{args}->{requirements};
    # Cookies to be added to output HTML
    $self->{cookies} = [];
    return $self;
}

=item invoke()

Process a request from the client. Should be called by
WebApp.pm::invokeCommand()

B<Arguments:> None

B<Returns:> Nothing

=cut

sub invoke
{
    my $self = shift;

    try
    {
	$self->{logger}->info("Invoking %s module.", ref($self));
	$self->_handleRequirements();
    }
    catch GridShibCA::SessionExpiredException with
    {
	my $ex = shift;
	$ex->flush();
	$self->_handleExpiredSession();
    }
    catch GridShibCA::SessionException with
    {
	my $ex = shift;
	$ex->flush();
	$self->_handleMissingSession();
    }
    otherwise
    {
	my $ex = shift;
	handleError("Error with request: " . $ex->text(),
	    -exception=>$ex);
    };

    # Any exception from this should be handled by WebApp.pm
    $self->doCommand();
}

=item doCommand()

This method should be overridden by the child class and implement the
actual work of the module.

B<Arguments:> None

B<Returns:> 1 on success, 0 on error.

May throw exceptions of any sort.

=cut

sub doCommand
{
    my $self = shift;

    return 1;
}

=back

=cut

######################################################################

=head2 Internal methods

These internal methods not intended for external use.

=over 4

=item _handleExpiredSession

Handle situation when session is required but has expired.
Displays SessionExpired template.

B<Arguments:> None

B<Returns:> Doesn't

=cut

sub _handleExpiredSession
{
    my $self = shift;

    $self->{logger}->debug("Handling expired session");
    my $authnMethod = $self->{session}->param("AuthnMethod");
    my $authnURL = $self->{config}->getParam("AuthMethods", $authnMethod);
    $self->_displayHTML(
	"SessionExpired",
	AuthnMethod => $authnMethod,
	AuthnURL => $authnURL,
	"-status" => 401);
    $self->_exit();
}

=item _handleMissingSession

Handle situation when session is required but is missing.
Displays SessionMissing template.

B<Arguments:> None

B<Returns:> Doesn't

=cut

sub _handleMissingSession
{
    my $self = shift;

    $self->{logger}->debug("Handling missing session");
    my %signOnMethods = $self->{config}->getSection("AuthMethods");
    $self->_displayHTML(
	"SessionMissing",
	"SignOnMethods" => \%signOnMethods,
	"-status" => 401);
    $self->_exit();
}

=item _addCookie()

Add cookie to output HTML.

B<Arguments>: CGI::cookie() instance

B<Returns:> Nothing

=cut

sub _addCookie
{
    my $self = shift;
    my $cookie = shift ||
	throw GridShibCA::Exception("Missing cookie argument");
    my $cookies = $self->{cookies};
    push(@$cookies, $cookie);
}

=item _displayHTML()

Display HTML template with given name.

B<Arguments:>

C<$filename> Name of HTML template in gridshib-ca.conf Templates section.

C<%mappings> Mappings for subsitution.  Can include -status, which
will be used for the HTTP status of the response.

B<Returns:> True on success, False on error.

=cut

sub _displayHTML
{
    my $self = shift;
    my $templateName = shift;
    my %mappings = @_;

    my $template = $self->{config}->getHTMLTemplate($templateName);
    $template->setCookies($self->{cookies});
    if (defined($mappings{-status}))
    {
	$self->{logger}->debug("Setting response status to %s",
			       $mappings{-status});
	$template->setStatus($mappings{-status});
    }
    
    # If we have an established session, add session values to mappings
    # for the template.
    if (defined($self->{session}) and
	$self->{session}->established())
    {
	$mappings{SessionId} = $self->{session}->id();
	$mappings{UserId} = $self->{session}->param("UserId");
	$mappings{IdpId} = $self->{session}->param("Idp");
	$mappings{session} = $self->{session}->params();
    }
    $template->addMappings(%mappings);

    my $html = $template->getText();
    print $html;

    return 1;
}

=item _displayHTMLError()

Display HTML template with given name.

B<Arguments:>

C<$message> Error message. 

C<%mappings> Optional additional mappings for subsitution.  Can
include -status, which will be used for the HTTP status of the
response. If a status is not give, 501 is used.

B<Returns:> Nothing

=cut

sub _displayHTMLError
{
    my $self = shift;
    my $message = shift;
    my %mappings;
    if (scalar(@_) > 0)
    {
	%mappings = @_;
    }
    else
    {
	%mappings = ();
    }
    $mappings{Error} = $message;
    if (!defined($mappings{-status}))
    {
	$mappings{-status} = 501;
    }
    $self->_displayHTML("ErrorTemplate", %mappings);
}

=item _defaultRequirements()

Return the default requirements for this command.

B<Arguments:> None

B<Returns:> Default requirements as bitmask

=cut

sub _defaultRequirements
{
    our $DEFAULT_REQUIREMENTS;
    return $DEFAULT_REQUIREMENTS;
}

=item _handleRequirements()

Handle the requirements of this command as specified by the
'requirements' element of the configuration for the command in
gridshib-ca.conf or as set by setRequirements().

B<Arguments:> None

B<Returns:> Nothing, throws exception on error.

=cut

sub _handleRequirements
{
    my $self = shift;

    our (
	$OPTIONAL_HTTPS,
	$OPTIONAL_SESSION,
	$REQUIRE_CSRF_CHECK,
	$REQUIRE_POST,
	);

    my $requirements = $self->_defaultRequirements();
    my $requirementsString = $self->{requirements};
    if (defined($requirementsString))
    {
	$requirements = $self->_parseRequirementsString($requirementsString);

    }
    # else just use defaults.

    # Sanity check
    if (!defined($requirements))
    {
	throw GridShibCA::RequirementsException("Requirements is undefined.");
    }

    $self->{logger}->debug("Checking request requirements (0x%X)",
			   $requirements);

    # Check for HTTPS
    if (!($requirements & $OPTIONAL_HTTPS))
    {
	if ($self->_checkHTTPS())
	{
	    $self->{logger}->debug("Verified HTTPS protection.");
	}
	else
	{
	    throw GridShibCA::RequirementsException(
		"Deployment error: HTTPS protection required but not provided");
	}
    }

    # Check for Session: We always try to establish session, but it's
    # only a fatal error if $OPTIONAL_SESSION is not set or something
    # unexpected happens.
    try
    {
	$self->{logger}->debug("Checking for session.");
	$self->{session} = $self->{config}->getCGISession();
	$self->{session}->fromCGI($self->{cgi});
    }
    catch GridShibCA::SessionExpiredException with
    {
	my $ex = shift;
	if ($requirements & $OPTIONAL_SESSION)
	{
	    # TODO: I think we really want to still set $self->{session}
	    # here so that we have access to the old session information.
	    # But that requires changes to CGISession.pm
	    $self->{logger}->debug("Session expired.");
	    $ex->flush();
	}
	else
	{
	    $ex->throw();
	}
    }
    catch GridShibCA::SessionException with
    {
	my $ex = shift;
	if ($requirements & $OPTIONAL_SESSION)
	{
	    $self->{logger}->debug("Failed to establish session.");
	    $ex->flush();
	}
	else
	{
	    $ex->throw();
	}
    }
    otherwise
    {
	my $ex = shift;
	$self->{logger}->logException($ex);
	throw GridShibCA::RequirementsException(
	    "Unexpected error establishing session: " . $ex->text());
    };

    if ($requirements & $REQUIRE_CSRF_CHECK)
    {
	$self->{logger}->debug("CSRF check");
	my $csrf = $self->{config}->getCSRF();
	$csrf->check();
    }

    if ($requirements & $REQUIRE_POST)
    {
	$self->{logger}->debug("Checking for POST");
	my $method = $self->{cgi}->request_method();
	if ($method ne "POST")
	{
	    throw GridShibCA::RequirementsException(
		"POST required but \"$method\" used.");
	}
    }
    $self->{logger}->debug("Requirements check done.");
}

=item _parseRequirementsString()

Parse the requirments string from out configuration and return
its bitmask value. String is expected to be comma-delimed set of
tokens which are valid keys to %RequiremetsMappings.

B<Arguments:> Requirement string

B<Returns:> Requirements as bitmask.

=cut

sub _parseRequirementsString
{
    my $self = shift;
    my $string = shift ||
	throw GridShibCA::RequirementsExceptions("Missing requirements string argument");

    our %RequirementsMappings;
    our $DEFAULT_REQUIREMENTS;

    my $requirements = $DEFAULT_REQUIREMENTS;
    my @tokens = split(/,/, $string);
    foreach my $token (@tokens)
    {
	my $requirement = $RequirementsMappings{$token};
	if (!defined($requirement))
	{
	    throw GridShibCA::RequirementsException("Unrecognized requirement \"$token\"");
	}
	$requirements |= $requirement;
    }
    return $requirements;
}

=item _checkHTTPS

Check and make sure request is over https.

B<Arguments:> None

B<Returns:> 1 if over https, zero otherwise.

=cut

sub _checkHTTPS
{
    my $self = shift;

    my $result = 0;
    my $url = $self->{cgi}->url(-full=>1);
    if ($url =~ /^https:/)
    {
	$result = 1;
    }
    else
    {
	$result = 0;
	$self->{logger}->err("Check for HTTPS failed. URL is %s", $url);
    }

    return $result;
}

=item _redirect()

Redirect the user to the given URL.

B<Arguments:> URL as string

B<Returns:> Nothing

=cut

sub _redirect
{
    my $self = shift;
    my $url = shift ||
	throw GridShibCA::Exception("Missing url argument");
    print $self->{cgi}->redirect($url);
}

=item _exit()

Exit. Does nothing special, just here for future hook point.

B<Arguments:> Options status, zero is default.

B<Returns:> Doesn't.

=cut

sub _exit
{
    my $self = shift;
    my $status = shift || 0;
    $self->{logger}->debug("Exiting.");
    exit($status);
}

=back

=cut

######################################################################

package GridShibCA::RequirementsException;

# Inherit GridShibCA::Exception
use base qw(GridShibCA::Exception);

=head1 GridShibCA::RequirementsException

Class for exceptions caused by requirement errors.

=cut

######################################################################

=head2 Version

GridShib-CA version @PACKAGE_VERSION_STRING@

=cut

# Return true for import/use
1;

######################################################################
### Local Variables: ***
### mode:perl ***
### End: ***
