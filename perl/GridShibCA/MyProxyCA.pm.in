#!@PERL@ @PERL_ARGS@
######################################################################
#
# $Id$
#
# Use MyProxy to act as a CA and create certificates for the
# GridShib-CA.
#
######################################################################
# Copyright 2006 The Board of Trustees of the University of Illinois.
# All rights reserved.

# Developed by:

#   The GridShib Project
#   National Center for Supercomputing Applications
#   University of Illinois
#   http://gridshib.globus.org/

# Permission is hereby granted, free of charge, to any person obtaining
# a copy of this software and associated documentation files (the
# "Software"), to deal with the Software without restriction, including
# without limitation the rights to use, copy, modify, merge, publish,
# distribute, sublicense, and/or sell copies of the Software, and to
# permit persons to whom the Software is furnished to do so, subject to
# the following conditions:

#   Redistributions of source code must retain the above copyright
#   notice, this list of conditions and the following disclaimers.

#   Redistributions in binary form must reproduce the above copyright
#   notice, this list of conditions and the following disclaimers in the
#   documentation and/or other materials provided with the distribution.

#   Neither the names of the National Center for Supercomputing
#   Applications, the University of Illinois, nor the names of its
#   contributors may be used to endorse or promote products derived from
#   this Software without specific prior written permission.

# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
# EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
# MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
# IN NO EVENT SHALL THE CONTRIBUTORS OR COPYRIGHT HOLDERS BE LIABLE FOR
# ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF
# CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION
# WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS WITH THE SOFTWARE.
######################################################################

=head1 GridShibCA::MyProxyCA

Implementation of L<GridShibCA::CA Class|GridShibCA::CA> based on MyProxy.
See that Module for a description of methods.

=head2 Configuration Parameters

This class uses the following values from F<gridshib-ca.conf>:

=over 4

=item MyProxyHostname

Hostname of MyProxy Server

=item MyProxyPort

Port of MyProxy Server

=item MyProxyClientCert

Certificate to use to authenticate to MyProxy

=item MyProxyClientKey

Key to use to authenticate to MyProxy

=back

=cut

package GridShibCA::MyProxyCA;

# Inherit CA class
use GridShibCA::CA;
@ISA = ("GridShibCA::CA");

use Globus::MyProxyClient;

use GridShibCA::Config;

=head1 Methods

=pver 4

=item new()

Create a new interface to a MyProxy CA.

B<Arguments:>

C<$username> - name of user as string

C<$idp> - idp of user as string

B<Returns:> CA instance

=cut

sub new
{
    my $class = shift;
    my $self = $class->SUPER::new(@_);
    $self->{config} = GridShibCA::Config->new();
    $self->{logger} = $self->{config}->getLogger();
    $self->{host} = $self->{config}->getParam("MyProxyHostname");
    $self->{port} = $self->{config}->getParam("MyProxyPort");
    $self->{cert} = $self->{config}->getParam("MyProxyClientCert");
    $self->{key} = $self->{config}->getParam("MyProxyClientKey");
    bless($self, $class);
    return $self;
}

=item signRequest()

Given a certificate request return a certificate

B<Arguments:> PEM-encoded PKCS10 certificate request as string.

B<Returns:> Certificate as Crypt::OpenSSL::X509 object

=cut

sub signRequest
{
    my $self = shift;
    my $certificateRequest = shift;

    my $client = Globus::MyProxyClient->new(
	ServerHost => $self->{host},
	ServerPort => $self->{port},
	ClientCert => $self->{cert},
	ClientKey => $self->{key});

    if (!defined($client))
    {
	throw GridShibCA::CAException("Error creating MyProxyClient object: %s",
				      Globus::MyProxyClient::errstr());
    }
    my $reqPEM = $self->_convertReqPEMtoDER($certificateRequest);
    my $lifetime = $self->{lifetime};
    my $dn = $self->getOpenSSLDN();
    # This isn't used since we're authenticating with a certificate
    # and private key
    my $passphrase = "Dummy";
    $self->{logger}->debug("MyProxy request: DN = %s lifetime = %d",
			   $dn, $lifetime);
    my $cred = $client->getCred(
	Username => $dn,
	Passphrase => $passphrase,
	Lifetime => $lifetime,
	CertReq => $reqPEM,
	CertReqFormat => "DER");

    if (!defined($cred))
    {
	throw GridShibCA::CAException("Error getting credential from MyProxy: %s",
				      $client->errstr());
    }

    my @certs = $cred->getDERCerts();
    if ($#certs > 1)
    {
	throw GridShibCA::CAException("Got a certificate chain from MyProxy of length greater than 1 (%d)", $#certs);
    }
    my $cert = $self->_convertCertDERtoPEM($certs[0]);
    my $x509 = $self->_X509fromPEM($pemCert);
    return $x509;
}

=back

=cut

######################################################################

=head1 Internal methods

These internal methods not intended for external use.

=over 4

=item _convertReqPEMtoDER

Convert PEM-encoded PKCS10 request to DER.

B<Arguments:> Certificate request as PEM-encoded PKCS10 string.

B<Returns:> Certificate request as DER-encoded string.

=cut

sub _convertReqPEMtoDER {
    my $self = shift;
    my $reqPEM = shift;
    my @args = ();

    # Uses OpenSSL as there doesn't seem to be a native way to accomplish.
    push(@args, "req");
    push(@args, "-inform", "PEM");
    push(@args, "-outform", "DER");
    my $openssl = $self->{config}->getParam("OpenSSL");
    my $cmd = $self->{config}->getCommand($openssl, @args);
    $cmd->setInput($reqPEM);
    if (!$cmd->exec())
    {
	throw GridShibCA::CAException("Trying to convert request from PEM to DER: OpenSSL returned non-zero.");
    }
    return $cmd->getOutput();
}

=item _convertCertDERtoPEM

Convert DER encoded X.509 certificate to PEM.

B<Arguments:> DER-encoded X.509 certificate.

B<Returns:> Certificate as PEM-encoded string.

=cut

sub _convertCertDERtoPEM {
    my $self = shift;
    my $certDER = shift;
    my @args = ();

    # Uses OpenSSL as there doesn't seem to be a native way to accomplish.
    push(@args, "x509");
    push(@args, "-inform", "DER");
    push(@args, "-outform", "PEM");
    my $openssl = $self->{config}->getParam("OpenSSL");
    my $cmd = $self->{config}->getCommand($openssl, @args);
    $cmd->setInput($certDER);
    if (!$cmd->exec())
    {
	throw GridShibCA::CAException("Error converting certificate from DER to PEM: OpenSSL returned non-zero");
    }
    return $cmd->getOutput();
}

=back

=head1 Version

GridShib-CA version @PACKAGE_VERSION_STRING@ ($Id$)

=cut

# Return true for import/use
1;

######################################################################
### Local Variables: ***
### mode:perl ***
### End: ***
