######################################################################
#
# This file is part of the GriShib-CA distribution.  Copyright
# 2006-2009 The Board of Trustees of the University of
# Illinois. Please see LICENSE at the root of the distribution.
#
######################################################################

=head1 GridShibCA::UserIdentity

Object wrapper for user's identity.

=cut

package GridShibCA::UserIdentity;
use GridShibCA::Config;

=head2 Methods

=over 4

=item new()

Create a new UserIdentity object.

B<Arguments:> Dictionary with the following values:

C<-authMethod>  Authentication method

C<-userId>      User identity

C<-idpId>       Identity identity

C<-clientHost>  User's host at authentication time.

C<-attributes>  Optional reference to dictionary of attributes (key/value pairs)

B<Returns:> UserIdentity instance

Throws GridShibCA::UserIdentityException on error.

=cut

sub new
{
    my $class = shift;
    my %args = @_;
    my $self = {};
    $self->{authMethod} = $args{-authMethod} ||
        throw GridShibCA::UserIdentityException("Missing authMethod argument");
    $self->{userId} = $args{-userId} ||
        throw GridShibCA::UserIdentityException("Missing userId argument");
    $self->{idpId} = $args{-idpId} ||
        throw GridShibCA::UserIdentityException("Missing idpId argument");
    $self->{clientHost} = $args{-clientHost} ||
        throw GridShibCA::UserIdentityException("Missing clientHost argument");
    $self->{attributes} = $args{-attributes} || {};
    bless($self, $class);
    return $self;
}

=item authMethod()

Return a string identifying the authentication method used by the user.

B<Arguments:> None

B<Returns:> Authentication method as string.

=cut

sub authMethod()
{
    my $self = shift;
    return $self->{authMethod};
}

=item userId()

Return the unchecked, tainted user id.

B<Arguments:> None

B<Returns:> User id as string.

=cut

sub userId
{
    my $self = shift;
    return $self->{userId};
}

=item idpId()

Returns the unchecked, tainted identity provider identifier.

B<Arguments:> None

B<Returns:> IdP as string.

=cut

sub idpId
{
    my $self = shift;
    return $self->{idpId};
}

=item clientHost

B<Arguments:> None

B<Returns:> Client hostname at authentication tip.

=cut

sub clientHost
{
    my $self = shift;
    return $self->{clientHost};
}

=item attributes

Returns reference to dictionary of attributes, which are unconstrained
key/value combinations.

B<Arguments:> None

B<Returns:> Hash reference.

=cut

sub attributes
{
    my $self = shift;
    return $self->{attributes};
}

=back

=cut

######################################################################

package GridShibCA::UserIdentityException;

# Inherit GridShibCA::Exception
use base qw(GridShibCA::Exception);

=head2 GridShibCA::UserIdentityException

Class for exceptions caused by user identity object errors.

=cut

######################################################################

=head2 Version

GridShib-CA version @PACKAGE_VERSION_STRING@

=cut

# Return true for import/use
1;

######################################################################
### Local Variables: ***
### mode:perl ***
### End: ***
