#!@PERL@ @PERL_ARGS@
######################################################################
#
# $Id$
#
# GridShib CA interface for CGI. This is mostly a wrapper around
# the Perl CGI module.
#
######################################################################
# Copyright 2006 The Board of Trustees of the University of Illinois.
# All rights reserved.

# Developed by:

#   The GridShib Project
#   National Center for Supercomputing Applications
#   University of Illinois
#   http://gridshib.globus.org/

# Permission is hereby granted, free of charge, to any person obtaining
# a copy of this software and associated documentation files (the
# "Software"), to deal with the Software without restriction, including
# without limitation the rights to use, copy, modify, merge, publish,
# distribute, sublicense, and/or sell copies of the Software, and to
# permit persons to whom the Software is furnished to do so, subject to
# the following conditions:

#   Redistributions of source code must retain the above copyright
#   notice, this list of conditions and the following disclaimers.

#   Redistributions in binary form must reproduce the above copyright
#   notice, this list of conditions and the following disclaimers in the
#   documentation and/or other materials provided with the distribution.

#   Neither the names of the National Center for Supercomputing
#   Applications, the University of Illinois, nor the names of its
#   contributors may be used to endorse or promote products derived from
#   this Software without specific prior written permission.

# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
# EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
# MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
# IN NO EVENT SHALL THE CONTRIBUTORS OR COPYRIGHT HOLDERS BE LIABLE FOR
# ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF
# CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION
# WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS WITH THE SOFTWARE.
######################################################################

=head1 GridShibCA::CGIinterface

Wrapper around standard PERL CGI
(L<http://search.cpan.org/dist/CGI.pm/CGI.pm>) interface with
convienence functions for the GridShibCA.

=head2 Configuration Parameters

This class uses the following values from F<gridshib-ca.conf>:

=over 4

=item HTMLFooter

Path to file to use as HTML footer.

=item HTMLHeader

Path to file to use as HTML header.

=back

=cut

package GridShibCA::CGIinterface;
use GridShibCA::Config;

# Inherit CGI class
use CGI;
@ISA = ("CGI");

my $config = GridShibCA::Config->new();
$logger = $config->getLogger();

=head2 Methods

=over 4

=item getCertificateRequest()

Returns the certificate request from POST.

B<Arguments:> None

B<Returns:> Certificate request, or undef if not present

=cut

sub getCertificateRequest
{
    my $self = shift;
    return $self->param('certificateRequest');
}

=item getUserId()

Return the user id as reported by Shibboleth through REMOTE_USER.
Clean the user id so it can pass taint checking.

B<Arguments:> None

B<Returns:> User id as string, or under if unknown

=cut

sub getUserId
{
    my $self = shift;
    my $remote_user = $self->remote_user();
    if (!defined($remote_user))
    {
	return undef;
    }
    # Clean user name (from perlsec manual)
    my $userId = undef;
    if ($remote_user =~ /^([-\@\w.]+)$/)
    {
	$userId = $1;
    }
    else
    {
	$logger->err("Illegal characters detected in REMOTE_USER ($remote_user)");
	return undef;
    }
    return $userId;
}

=item getIdP()

Returns the Shibboleth identity provider

B<Arguments:> None

B<Returns:> IdP as string, or undef if unknown

=cut

sub getIdP
{
    my $idp = undef;
    if (defined($ENV{"HTTP_SHIB_IDENTITY_PROVIDER"}))
    {
	$idp = $ENV{"HTTP_SHIB_IDENTITY_PROVIDER"};
    }
    # Clean Idp Id (from perlsec manual)
    if ($idp =~ /^([-\/:\@\w.]+)$/)
    {
	$idp = $1;
    }
    else
    {
	$logger->err("Illegal characters detected in IdP Id ($idp)");
	return undef;
    }
    return $idp;
}

=item getShibSession()

Returns the Shibboleth session cookie as a "variable=value" string.

B<Arguments:> None

B<Returns:> Shibboleth session or undef if cannot be found

=cut

sub getShibSession
{
    use CGI::Cookie;

    my %cookies = fetch CGI::Cookie;
    my $shibsession = undef;
    foreach my $key (keys %cookies)
    {
	if ($key =~ /_shibsession_/)
	{
	    $shibsession = $key . "=" . $cookies{$key}->value;
	    last;
	}
    }
    return $shibsession;
}

=item getToken()

Returns the token from the posted request.

B<Arguments:> None

B<Returns:> Token as string, or undef if cannot be found

=cut

sub getToken
{
    my $self = shift;
    return $self->param("token");
}

=item getPortalURL()

Returns the URL for a portal requesting a delegation

B<Arguments:> None

B<Returns:> URL as string, or undef if not found

=cut

sub getPortalURL
{
    my $self = shift;
    return $self->param("portalURL");
}

=item getPortalData()

Returns opaque portal data to be passed back to portal.

B<Arguments:> None

B<Returns:> Data as string, empty string if not found.

=cut

sub getPortalData
{
    my $self = shift;
    return $self->param("portalData") || "";
}

=item getRequestedCredLifetime

Returns the requested credential lifetime in seconds.

B<Arguments:> None

B<Returns:> Lifetime in seconds, 0 if no request found.

=cut

sub getRequestedCredLifetime
{
    my $self = shift;
    my $lifetime = $self->param("lifetime");
    if (!defined($lifetime) or ($lifetime eq "default"))
    {
	$lifetime = $config->getParam("DefaultCredLifetime");
    }
    elsif ($lifetime eq "specified")
    {
	$lifetime = $self->param("specifiedLifetime");
	my $unit = $self->param("lifetimeUnit");
	if (defined($unit) && ($unit eq "hours"))
	{
	    $lifetime *= 3600;
	}
    }
    # Clean lifetime for taint mode
    if ($lifetime =~ /^(\d+)$/)
    {
	$lifetime = $1;
    }
    else
    {
	$logger->err("Illegal characters detected in lifetime ($lifetime)");
	return $config->getParam("DefaultCredLifetime");
    }
    return $lifetime;
}

=item requestedTrustedCAUpdate

Did the user request update of their trusted CAs?

B<Arguments:> None

B<Returns:> 1 if yes, 0 otherwise

=cut

sub requestedTrustedCAUpdate
{
    my $self = shift;
    my $requestedCAUpdate = $self->param("installTrustedCAs");
    if (defined($requestedCAUpdate) and
	($requestedCAUpdate eq "true"))
    {
	return 1;
    }
    return 0;
}

=item getHTMLFromFile()

Returns HTML from given filename.

B<Arguments:> Filename

B<Returns:> HTML as string

=cut

sub getHTMLFromFile
{
    my $self = shift;
    my $filename = shift;
    my $htmlString = "";
    if (!open(HTMLFile, $filename))
    {
	$logger->err("Could not open HTML file %s for reading: %s",
		     $filename, $!);
	# Return empty string to avoid other errors
	return "";
    }
    while(<HTMLFile>)
    {
	$htmlString .= $_;
    }
    close(HTMLFile);
    return $htmlString;
}

=item getHTMLFooter()

Returns HTML footer for GridShibCA web pages.

B<Arguments:> None

B<Returns:> HTML as string, undef on error

=cut

sub getHTMLFooter
{
    my $self = shift;
    return $self->getHTMLFromFile($config->getParam("HTMLFooter"));
}


=item getHTMLHeader()

Returns HTML header for GridShibCA web pages.

B<Arguments:> None

B<Returns:> HTML as string, undef on error

=cut

sub getHTMLHeader
{
    my $self = shift;
    return $self->getHTMLFromFile($config->getParam("HTMLHeader"));
}

=back

=head2 Version

GridShib-CA version @PACKAGE_VERSION_STRING@ ($Id$)

=cut

# Return true for import/use
1;

######################################################################
### Local Variables: ***
### mode:perl ***
### End: ***
