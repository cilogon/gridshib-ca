#!@PERL@ @PERL_ARGS@
######################################################################
#
# $Id$
#
# This script is invoked by the Java Web Start CredentialRetriever
# application. It comsumes the token created by the "login" script,
# contacts MyProxy to generate a X509 credential and returns that
# credential to CredentialRetriever.
#
######################################################################
# Copyright 2006 The Board of Trustees of the University of Illinois.
# All rights reserved.

# Developed by:

#   The GridShib Project
#   National Center for Supercomputing Applications
#   University of Illinois
#   http://gridshib.globus.org/

# Permission is hereby granted, free of charge, to any person obtaining
# a copy of this software and associated documentation files (the
# "Software"), to deal with the Software without restriction, including
# without limitation the rights to use, copy, modify, merge, publish,
# distribute, sublicense, and/or sell copies of the Software, and to
# permit persons to whom the Software is furnished to do so, subject to
# the following conditions:

#   Redistributions of source code must retain the above copyright
#   notice, this list of conditions and the following disclaimers.

#   Redistributions in binary form must reproduce the above copyright
#   notice, this list of conditions and the following disclaimers in the
#   documentation and/or other materials provided with the distribution.

#   Neither the names of the National Center for Supercomputing
#   Applications, the University of Illinois, nor the names of its
#   contributors may be used to endorse or promote products derived from
#   this Software without specific prior written permission.

# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
# EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
# MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
# IN NO EVENT SHALL THE CONTRIBUTORS OR COPYRIGHT HOLDERS BE LIABLE FOR
# ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF
# CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION
# WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS WITH THE SOFTWARE.
######################################################################

use GridShibCA::Config;

# Run safely
$ENV{PATH} = "/bin";
umask (0077);

my $config = GridShibCA::Config->new();

######################################################################
#
# HTTP Status codes returned in case of error
#

my $HTTP_BAD_REQUEST = 400;
my $HTTP_UNAUTHORIZED = 401;
my $HTTP_INTERNAL_ERROR = 500;

######################################################################

sub errorExit
{
    # HTTP status code
    my $status = shift;
    # Error message
    my $format = shift;
    my $string = sprintf($format, @_);

    print $cgi->header(
	-status=>sprintf("%d %s", $status, $string),
	-type=>"text/plain");

    print "ERROR: $string\n";
    print STDERR "ERROR: $string\n";

    $logger->err("ERROR: %s", $string);

    exit(0);
}

######################################################################
#
# Initialize Logging
#

$logger = $config->getLogger();

if (!defined($logger))
{
    # No good way to report this
    die "Could not initialize Logger: " . $config->getError();
}

######################################################################

$cgi = $config->getCGI();

if (!defined($cgi))
{
    errorExit($HTTP_INTERNAL_ERROR,
	      "Could not initialize CGI interface: "
	      . $config->getError());
}

$user = $cgi->getUserId();

if (!defined($user))
{
    errorExit($HTTP_UNAUTHORIZED, "REMOTE_USER not defined.");
}

my $remoteAddr = $cgi->remote_host();

if (!defined($remoteAddr))
{
    errorExit($HTTP_INTERNAL_ERROR, "REMOTE_ADDR not defined.");
}

my $idp = $cgi->getIdP();

if (!defined($idp))
{
    errorExit($HTTP_INTERNAL_ERROR, 
	      "Could not determine Shibboleth identity provider for user");
}

$logger->info("CONNECTION:%s:%s:%s", $user, $remoteAddr, $idp);

######################################################################
#
# Get POST data
#

my $certificateRequest = $cgi->getCertificateRequest();

if (!defined($certificateRequest))
{
    errorExit($HTTP_BAD_REQUEST, "Certificate request missing");
}
if ($certificateRequest eq "")
{
    errorExit($HTTP_BAD_REQUEST, "Certificate request empty");
}

my $token = $cgi->getToken();

if (!defined($token))
{
    errorExit($HTTP_BAD_REQUEST, "Token missing from request");
}

my $portalURL = $cgi->getPortalURL();

######################################################################
#
# Validate token
#
# This ensures that this request was routed here through a trusted
# CGI script and not, e.g., malicious javascript.

my $tokenGenerator = $config->getTokenGenerator($user,
						$remoteAddr);

if (!defined($tokenGenerator))
{
    errorExit($HTTP_INTERNAL_ERROR,
	      "Could not initialize token generator: "
	      . $config->getError());
}

if (!$tokenGenerator->verify($token))
{
    errorExit($HTTP_BAD_REQUEST, "Error verifying token: "
	      . $tokenGenerator->getError());
}

######################################################################
#
# Get DN for logging
#

my $ca = $config->getCA($user, $idp);
if (!defined($ca))
{
    errorExit($HTTP_INTERNAL_ERROR,
	      "Could not instantiate CA interface "
	      . $config->getError());
}
my $DN = $ca->getDN();
if (!defined($DN))
{
    errorExit($HTTP_INTERNAL_ERROR,
	      "Couldn't determine DN for user: " . $ca->getError());
}

######################################################################
#
# Authorize request
#

my $authorization = $config->getAuthorization();

if (!$authorization->authorizeUser($user, $idp))
{
    errorExit($HTTP_UNAUTHORIZED,
	      "Authorization failed: User %s (Idp %s): %s",
	      $user, $idp, $authorization->getError());
}

if (defined($portalURL))
{
    if (!$config->getParamBoolean("allowPortalDelegation"))
    {
	errorExit($HTTP_UNAUTHORIZED,
		  "Portal delegation disabled.");
    }

    if (!$authorization->authorizePortal($portalURL))
    {
	errorExit($HTTP_UNAUTHORIZED,
		  "Authorization failed: Portal (%s) not authorized: %s",
		  $portalURL, $authorization->getError());
    }
}

######################################################################
#
# And generate the credentials, which will be written to stdout.
#

my $certificate = $ca->signRequest($certificateRequest);

if (!defined($certificate))
{
    errorExit($HTTP_INTERNAL_ERROR,
	      "Error signing certificate request: " . $ca->getError());
}

# XXX Add serial number to logging
$logger->info("ISSUE:%s (%s):%s:%s", $user, $idp, $remoteAddr, $DN);

my $certRegistry = $config->getCertRegistry();

if (defined($certRegistry))
{
    if (!$certRegistry->registerCert($user, $idp, $DN, $certificate))
    {
	errorExit($HTTP_INTERNAL_ERROR,
		  "Error registering certificate: %s",
		  $certRegistry->getError());
    }
}

# Success, output credential

if (!defined($portalURL))
{
    # No portal URL, we are outputing to simple client as text
    print $cgi->header("text/plain");
    print $certificate;
}
else
{
    # We are outputting a form to allow certificate to be posted
    # to a portal
    my $portalData = $cgi->getPortalData();
    my $htmlHeader = $cgi->getHTMLHeader();
    my $htmlFooter = $cgi->getHTMLFooter();
    print $cgi->header();
    print <<"EOF";
<html>
<head>
  <title>GridShib CA Welcome</title>
  <META HTTP-EQUIV=\"Pragma\" CONTENT=\"no-cache\">
  <META HTTP-EQUIV=\"Expires\" CONTENT=\"-1\">
</head>
$htmlHeader

<center>
<form action="$portalURL" method=post>
<input name="status" type="hidden" value="success">
<input name="certificate" type="hidden" value="$certificate">
<input name="portalData" type="hidden" value="$portalData">
Your certificate is ready to be returned to the portal.
<p>
<input value="Click here finish delegation and return to the portal" type="submit">
</form>

$htmlFooter
</html>
EOF
}

######################################################################
#
# And we're done
#

exit(0);

### Local Variables: ***
### mode:perl ***
### End: ***
