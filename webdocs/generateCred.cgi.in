#!@PERL@ @PERL_ARGS@
######################################################################
#
# $Id$
#
# This script is invoked by the Java Web Start CredentialRetriever
# application. It comsumes the token created by the "login" script,
# contacts MyProxy to generate a X509 credential and returns that
# credential to CredentialRetriever.
#
######################################################################
# Copyright 2006 The Board of Trustees of the University of Illinois.
# All rights reserved.

# Developed by:

#   The GridShib Project
#   National Center for Supercomputing Applications
#   University of Illinois
#   http://gridshib.globus.org/

# Permission is hereby granted, free of charge, to any person obtaining
# a copy of this software and associated documentation files (the
# "Software"), to deal with the Software without restriction, including
# without limitation the rights to use, copy, modify, merge, publish,
# distribute, sublicense, and/or sell copies of the Software, and to
# permit persons to whom the Software is furnished to do so, subject to
# the following conditions:

#   Redistributions of source code must retain the above copyright
#   notice, this list of conditions and the following disclaimers.

#   Redistributions in binary form must reproduce the above copyright
#   notice, this list of conditions and the following disclaimers in the
#   documentation and/or other materials provided with the distribution.

#   Neither the names of the National Center for Supercomputing
#   Applications, the University of Illinois, nor the names of its
#   contributors may be used to endorse or promote products derived from
#   this Software without specific prior written permission.

# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
# EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
# MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
# IN NO EVENT SHALL THE CONTRIBUTORS OR COPYRIGHT HOLDERS BE LIABLE FOR
# ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF
# CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION
# WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS WITH THE SOFTWARE.
######################################################################

use Command;

# Run safely
$ENV{PATH} = "/bin";
umask (0077);

# Syslog parameters
$SYSLOG_ID = "@SYSLOG_ID@";
$SYSLOG_OPTS = "pid";
$SYSLOG_FACILITY = "@SYSLOG_FACILITY@";

$CA_PROG = "@GRIDSHIB_CA_PROG@";
$LOGGER_PROG = "@GRIDSHIB_LOGGER_PROG@";
$AUTHZ_PROG = "@GRIDSHIB_AUTHZ_PROG@";

$OPENSSL = "@OPENSSL@";

######################################################################
#
# HTTP Status codes returned in case of error
#

my $HTTP_BAD_REQUEST = 400;
my $HTTP_UNAUTHORIZED = 401;
my $HTTP_INTERNAL_ERROR = 500;

######################################################################

sub errorExit($$)
{
    # HTTP status code
    my $status = shift;
    # Error message
    my $string = shift;

    print $cgi->header(
	-status=>sprintf("%d %s", $status, $string),
	-type=>"text/plain");

    print "ERROR: $string\n";
    print STDERR "ERROR: $string\n";
    syslog("err", "ERROR: %s", $string);
    exit(0);
}

######################################################################
#
# Initialize Logging
#

use Sys::Syslog;

openlog($SYSLOG_ID, $SYSLOG_OPTS, $SYSLOG_FACILITY);

######################################################################

use CGI;

$cgi = new CGI;

$user = $ENV{"REMOTE_USER"};

if (!defined($user))
{
    errorExit($HTTP_UNAUTHORIZED, "REMOTE_USER not defined.");
}

# Clean user name (from perlsec manual)
if ($user =~ /^([-\@\w.]+)$/)
{
    $user = $1;                     # $data now untainted
}
else
{
    errorExit($HTTP_BAD_REQUEST, "Username contains bad characters: $user");
}


my $remoteAddr = $ENV{"REMOTE_ADDR"};

if (!defined($remoteAddr))
{
    errorExit($HTTP_INTERNAL_ERROR, "REMOTE_ADDR not defined.");
}

syslog("info", "CONNECTION:%s:%s", $user, $remoteAddr);

######################################################################
#
# Get POST data
#

my $certificateRequest = $cgi->param('certificateRequest');

if (!defined($certificateRequest))
{
    errorExit($HTTP_BAD_REQUEST, "Certificate request missing");
}
if ($certificateRequest eq "")
{
    errorExit($HTTP_BAD_REQUEST, "Certificate request empty");
}

######################################################################
#
# Get DN for logging
#

# XXX Use array for here for arguments
open(Mapper, "$CA_PROG -n \"$user\"|") ||
    errorExit($HTTP_INTERNAL_ERROR, "Could not execute $CA_PROG: $!");

$DN = <Mapper>;
chomp($DN);
if (!close(Mapper))
{
    # DN carried error message in this case
    errorExit($HTTP_INTERNAL_ERROR,
	      "$CA_PROG returned non-zero(" . ($?<< 8) . ": $DN");
}

# Clean DN to satisfy perl taint checking.
if ($DN =~ /^([^;]*)$/)
{
    $DN = $1;
}
else
{
    errorExit($HTTP_BAD_REQUEST, "Invalid characters in DN: $DN");
}

######################################################################
#
# Check DN in certificate request and make sure it is correct
#

my @cmdArgs = ("req", "-text");

my $reqCmd = Command->new($OPENSSL, @cmdArgs);
$reqCmd->setInput($certificateRequest);
$reqCmd->exec() ||
    errorExit($HTTP_INTERNAL_ERROR, $reqCmd->getSTDERR());
my $reqText = $reqCmd->getSTDOUT();

$reqText =~ /Subject: (.*)$/;

my $reqDN = $1;

if (!defined($reqDN))
{
    errorExit($HTTP_INTERNAL_ERROR,
	      "Could not get DN from certificate request");
}

if ($reqDN != $DN)
{
    errorExit($HTTP_BAD_REQUEST,
	      "DN in request ($reqDN) not valid. Valid DN is: $DN");
}

######################################################################
#
# Callout to external authz program if it exists
#


if (-x $AUTHZ_PROG)
{
    my @args = ($AUTHZ_PROG, $user);
    my $status = system(@args);

    if ($status != 0)
    {
	if ($? == -1)
	{
	    syslog("err", "ERROR: %s failed: $!", $AUTHZ_PROG);
	    errorExit($HTTP_INTERNAL_ERROR,
		      "Authorization callout to $AUTHZ_PROG failed: $!");
	}
	else
	{
	    syslog("err", "ERROR: %s returned %d", $AUTHZ_PROG, ($? >> 8));
	    errorExit($HTTP_UNAUTHORIZED, "Authorization for $user denied.");
	}

    }
}


######################################################################
#
# And generate the credentials, which will be written to stdout.
#

syslog("info", "ISSUE:%s:%s:%s", $user, $remoteAddr, $DN);

my $cmd = Command->new($CA_PROG, $user);
$cmd->setInput($certificateRequest);
if (!$cmd->exec())
{
    errorExit($HTTP_INTERNAL_ERROR,
	      $cmd->getSTDERR());
}
my $certificate = $cmd->getSTDOUT();

# Success, print credential to STDOUT to be returned to client
print $cgi->header("text/plain");
print $certificate;

######################################################################
#
# Call external logging program if it exists
#
# XXX Needs to be revamped to parse X509 cert

if (-x $LOGGER_PROG)
{
    my @args = ($LOGGER_PROG, $user, $DN);
    my $status = system(@args);

    if ($status != 0)
    {
	if ($? == -1)
	{
	    syslog("err", "ERROR: %s failed: $!", $LOGGER_PROG);
	}
	else
	{
	    syslog("err", "ERROR: %s returned %d", $LOGGER_PROG, ($? >> 8));
	}
    }
}

######################################################################
#
# And we're done
#

exit(0);

### Local Variables: ***
### mode:perl ***
### End: ***
